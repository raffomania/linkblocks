set dotenv-load := true
set export := true

watch *args: development-cert migrate-database
    cargo bin systemfd --no-pid -s ${LISTEN} -- cargo bin cargo-watch -- cargo run start --listenfd {{args}}

run *args: development-cert migrate-database
    cargo run -- {{args}}

insert-demo-data: migrate-database
    cargo run -- insert-demo-data

start-database:
    #!/usr/bin/env bash
    set -euxo pipefail

    if podman ps --format "{{{{.Names}}" | grep -wq linkblocks_postgres; then
        echo "Database is running."
        exit
    fi

    if ! podman inspect linkblocks_postgres &> /dev/null; then
        podman create \
            --name linkblocks_postgres \
            --health-cmd="pg_isready" \
            --health-startup-cmd="pg_isready" --health-startup-interval=2s \
            -e POSTGRES_HOST_AUTH_METHOD=trust -e POSTGRES_DB=${DATABASE_NAME} \
            -p ${DATABASE_PORT}:5432 docker.io/postgres:15 \
            postgres
    fi

    podman start linkblocks_postgres

    podman wait --condition=healthy linkblocks_postgres

start-rauthy:
    #!/usr/bin/env bash
    set -euxo pipefail

    # TODO: extract helpers for repetitive podman tasks.
    if podman ps --format "{{{{.Names}}" | grep -wq linkblocks_rauthy; then
        echo "Rauthy is running."
        exit
    fi

    if ! podman inspect linkblocks_rauthy &> /dev/null; then
        podman create \
            --replace --name linkblocks_rauthy \
            --pull=missing \
            -e COOKIE_MODE=danger-insecure \
            -e PUB_URL=localhost:${RAUTHY_PORT} \
            -e LOG_LEVEL=info \
            -e LOCAL_TEST=true \
            -e BOOTSTRAP_ADMIN_EMAIL=admin@rauthy.localhost \
            -e BOOTSTRAP_ADMIN_PASSWORD_PLAIN=test \
            -e DATABASE_URL=sqlite:data/rauthy.db \
            -p ${RAUTHY_PORT}:8080 \
            ghcr.io/sebadob/rauthy:0.29.4
    fi

    podman start linkblocks_rauthy

stop-rauthy:
    podman stop linkblocks_rauthy

wipe-rauthy: stop-rauthy
    podman rm linkblocks_rauthy

stop-database:
    podman stop --ignore linkblocks_postgres

wipe-database: stop-database
    podman rm --ignore linkblocks_postgres
    # SQLX_OFFLINE: when migrating an empty db, checking queries against
    # it would fail during compilation
    SQLX_OFFLINE=true just migrate-database

migrate-database: start-database
    cargo run -- db migrate

exec-database-cli: start-database
    podman exec -ti -u postgres linkblocks_postgres psql ${DATABASE_NAME}

generate-database-info: start-database migrate-database
    cargo bin sqlx-cli prepare -- --all-targets

start-test-database:
    #!/usr/bin/env bash
    set -euxo pipefail

    if podman ps --format "{{{{.Names}}" | grep -wq linkblocks_postgres_test; then
        echo "Test database is running."
        exit
    fi

    if ! podman inspect linkblocks_postgres_test &> /dev/null; then
        podman create \
            --replace --name linkblocks_postgres_test --image-volume tmpfs \
            --health-cmd pg_isready --health-interval 10s \
            --health-startup-cmd="pg_isready" --health-startup-interval=2s \
            -e POSTGRES_HOST_AUTH_METHOD=trust -e POSTGRES_DB=${DATABASE_NAME_TEST} \
            -p ${DATABASE_PORT_TEST}:5432 --rm docker.io/postgres:16 \
            postgres \
            -c fsync=off \
            -c synchronous_commit=off \
            -c full_page_writes=off \
            -c autovacuum=off
    fi

    podman start linkblocks_postgres_test

    podman wait --condition=healthy linkblocks_postgres

test *args: start-test-database generate-database-info
    # SQLX_OFFLINE: Without it, `cargo test` would compile against the test db
    # which is always empty and only migrated inside the tests themselves.
    DATABASE_URL=${DATABASE_URL_TEST} SQLX_OFFLINE=true cargo test {{args}}

development-cert: (ensure-command "mkcert")
    mkdir -p development_cert
    test -f development_cert/localhost.crt || mkcert -cert-file development_cert/localhost.crt -key-file development_cert/localhost.key localhost linkblocks.localhost 127.0.0.1 ::1

# Run most of the CI checks locally. Convenient to check for errors before pushing.
ci-dev : migrate-database start-test-database && generate-sbom generate-database-info
    #!/usr/bin/env bash
    set -euxo pipefail

    export RUSTFLAGS="-D warnings"
    # Prevent full recompilations in the normal dev setup which has different rustflags
    export CARGO_TARGET_DIR="target_ci"

    cargo build --release

    just lint
    just format
    just test

# Build a production-ready OCI container using podman.
build-podman-container target="release":
    #!/bin/sh
    [[ "{{target}}" == "debug" ]] && cargo_flag="" || cargo_flag="--{{target}}"
    cargo build $cargo_flag

    podman build --format docker --platform linux/amd64 --manifest linkblocks -f Containerfile target/{{target}}

lint *args: reuse-lint
    cargo clippy {{args}} -- -D warnings

lint-fix *args: reuse-lint
    cargo clippy --fix {{args}}
    cargo fix --allow-staged --all-targets

reuse-lint: (ensure-command "reuse")
    reuse --root . lint

format:
    cargo +nightly fmt --all

generate-sbom:
    cargo bin cargo-cyclonedx --format json --describe binaries
    # Remove some fields that make the sbom non-reproducible.
    # https://github.com/CycloneDX/cyclonedx-rust-cargo/issues/556
    # https://github.com/CycloneDX/cyclonedx-rust-cargo/issues/514
    jq --sort-keys '.components |= sort_by(.purl) | del(.serialNumber) | del(.metadata.timestamp) | del(..|select(type == "string" and test("^path\\+file")))' linkblocks_bin.cdx.json > linkblocks.cdx.json
    rm linkblocks_bin.cdx.json

install-git-hooks:
    ln -srf pre-commit.sh .git/hooks/pre-commit

# Run extended checks that are not part of the normal CI pipeline.
check-extended: verify-msrv build-podman-container

verify-msrv: (ensure-command "cargo-msrv")
    cargo msrv verify

# Diagnose potential problems in the development environment.
doctor:
    #!/usr/bin/env bash

    just ensure-command "podman"
    just ensure-command "cargo"
    just ensure-command "cargo-bin"
    just ensure-command "mkcert"

    [[ -f .env ]] || echo ".env file is missing. Please copy .env.example and adjust it for your environment."

[private]
ensure-command +command:
    #!/usr/bin/env bash
    set -euo pipefail

    read -r -a commands <<< "{{ command }}"

    for cmd in "${commands[@]}"; do
        if ! command -v "$cmd" > /dev/null 2>&1 ; then
            printf "Couldn't find required executable '%s'\n" "$cmd" >&2
            exit 1
        fi
    done
